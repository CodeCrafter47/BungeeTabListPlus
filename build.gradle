buildscript {
    repositories {
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'ru.vyarus:gradle-quality-plugin:4.4.0'
    }
}

ext {
    spigotVersion = '1.11-R0.1-SNAPSHOT'
    bungeeVersion = '1.20-R0.2-SNAPSHOT'
    spongeVersion = '7.0.0'
    dataApiVersion = '1.0.2-SNAPSHOT'
}

allprojects {
    group = 'codecrafter47.bungeetablistplus'
    version = '3.6.5-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'ru.vyarus.quality'

    repositories {
        mavenCentral()
        maven {
            url = 'https://nexus.codecrafter47.dyndns.eu/content/repositories/public/'
        }

        maven {
            url = 'https://repo.codemc.org/repository/maven-public'
        }

        maven {
            url = 'https://repo.md-5.net/content/repositories/snapshots/'
        }

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }

        maven {
            url = 'https://papermc.io/repo/repository/maven-snapshots/'
        }

        maven {
            url = 'https://repo.spongepowered.org/maven'
        }

        maven {
            url = 'https://jitpack.io'
        }

        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    dependencies {

        compileOnly 'org.projectlombok:lombok:1.18.20'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
        testCompileOnly 'org.projectlombok:lombok:1.18.20'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
        compileOnly 'com.google.code.findbugs:jsr305:3.0.1'
    }

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    publishing {
        repositories {
            maven {
                if (project.version.endsWith("-SNAPSHOT")) {
                    url "https://nexus.codecrafter47.de/content/repositories/snapshots/"
                } else {
                    url "https://nexus.codecrafter47.de/content/repositories/releases/"
                }
                credentials {
                    username "${project.properties.mavenUser}"
                    password "${project.properties.mavenPassword}"
                }
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    quality {
        strict = false
    }

    tasks.withType(Javadoc) {
        failOnError false
    }
}
